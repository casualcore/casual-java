/*
 * Copyright (c) 2017 - 2018, The casual project. All rights reserved.
 *
 * This software is licensed under the MIT license, https://opensource.org/licenses/MIT
 */

//-*- coding: utf-8 -*-
plugins {
    id 'java'
    id 'jacoco'
    id 'groovy'
}

defaultTasks 'build'

group = 'se.laz.casual'

configurations {
    resourceAdapter
    resourceAdapterLib
}

dependencies {
    provided project(':casual:casual-api')
    implementation project(':casual:casual-inbound-api')
    implementation project(':casual:casual-internal')
    implementation project(':casual:casual-network')
    implementation project(':casual:casual-network-protocol')
    implementation project(':casual:casual-inbound')
    provided libs.netty
    provided libs.javaee_api
    resourceAdapter project(':casual:casual-internal')
    resourceAdapter project(':casual:casual-network')
    resourceAdapter project(':casual:casual-network-protocol')
    resourceAdapter project(':casual:casual-inbound-api')
    resourceAdapterLib libs.netty

    testCompile libs.hamcrest_all
    // for spock
    testCompile libs.groovy_all
    testCompile libs.spock_core
    testCompile libs.cglib_nodep
    testCompile libs.objenesis
}

task rar(type: Jar) {
    archiveName = "casual-jca.rar"
    manifest {
        attributes 'Implementation-Title': 'Casual Resource Adapter with XA', 'Implementation-Version': '1.0', 'Specification-Version': '1.0'
    }
    extension = 'rar'
    from(configurations.resourceAdapterLib){
	into('lib')
    }
    into('') {
        from jar
    }
    into('') {
        from configurations.resourceAdapter
    }
    into('') {
        from sourceSets.main.resources
    }

}

artifacts.add( "resourceAdapter", rar )
